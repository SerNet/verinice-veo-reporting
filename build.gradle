plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'groovy'
    id 'jacoco'
    id "com.github.spotbugs" version '4.7.0'
    id "com.github.hierynomus.license" version "0.15.0"
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'javax.validation:validation-api'
    implementation 'org.freemarker:freemarker:2.3.31'
    implementation 'com.vladsch.flexmark:flexmark-all:0.62.2'
    implementation 'org.keycloak:keycloak-authz-client:11.0.3'

    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

def continuousIntegrationBuild = rootProject.hasProperty('ci')

spotbugsTest.enabled = false
// write human readable report on normal builds
spotbugsMain.reports {
    // Jenkins needs xml reports to detect and display bugs.
    xml.enabled = continuousIntegrationBuild
    html.enabled = !continuousIntegrationBuild
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.openhtmltopdf' && details.requested.version == '1.0.0' && details.requested.name != 'openhtmltopdf-jsoup-dom-converter') {
            details.useVersion '1.0.1'
            details.because 'fixes bookmark handling (https://github.com/danfickle/openhtmltopdf/issues/408)'
        }
    }
}

license {
    header = file("templates/licenseHeader.txt")
    exclude "reports/**"
    exclude "templates/*"
    skipExistingHeaders = true
    ext["year"] = java.util.Calendar.getInstance().get(Calendar.YEAR)
    ext["author"] = "git config user.name".execute().text.trim() ?: "<name>"
}