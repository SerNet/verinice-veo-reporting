plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'groovy'
    id 'jacoco'
    id "com.github.spotbugs" version '4.7.1'
    id 'org.cadixdev.licenser' version '0.6.1'
    id "com.gorylenko.gradle-git-properties" version "2.3.1"
    id 'pmd'
}

repositories {
    mavenCentral()
}

def versions = [
    openhtmltopdf: '1.0.9',
    phfonts: '4.2.0',
]

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'javax.validation:validation-api'
    implementation 'org.freemarker:freemarker:2.3.31'
    implementation 'com.vladsch.flexmark:flexmark-all:0.62.2'
    implementation "com.openhtmltopdf:openhtmltopdf-core:$versions.openhtmltopdf"
    implementation "com.openhtmltopdf:openhtmltopdf-pdfbox:$versions.openhtmltopdf"
    implementation "com.openhtmltopdf:openhtmltopdf-slf4j:$versions.openhtmltopdf"
    implementation "com.helger.font:ph-fonts-api:$versions.phfonts"

    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly "com.helger.font:ph-fonts-open-sans:$versions.phfonts"

    testImplementation 'org.spockframework:spock-spring:2.0-groovy-3.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.codehaus.groovy:groovy-json'
    testImplementation 'org.codehaus.groovy:groovy-xml'
}

def continuousIntegrationBuild = rootProject.hasProperty('ci')

spotbugsTest.enabled = false
// write human readable report on normal builds
spotbugsMain.reports {
    // Jenkins needs xml reports to detect and display bugs.
    xml.enabled = continuousIntegrationBuild
    html.enabled = !continuousIntegrationBuild
}

pmd {
    toolVersion = '6.34.0'
    rulesMinimumPriority = 2
    consoleOutput = true
}

pmdTest.enabled = false

license {
    header = file("templates/licenseHeader.txt")
    newLine = false
    exclude "reports/**"
    exclude "templates/*"
    skipExistingHeaders = true
    ext["year"] = java.util.Calendar.getInstance().get(Calendar.YEAR)
    ext["author"] = "git config user.name".execute().text.trim() ?: "<name>"
    style {
        java = 'JAVADOC'
    }
}

springBoot {
    buildInfo {
        properties {
            if (rootProject.hasProperty('ciBuildNumer')) {
                additional = [
                    'ci.buildnumber': rootProject.getProperty('ciBuildNumer'),
                    'ci.jobname': rootProject.getProperty('ciJobName')
                ]
            }
        }
    }
}

test {
    useJUnitPlatform()
}
