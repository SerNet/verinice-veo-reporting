plugins {
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'groovy'
    id 'jacoco'
    id "com.github.spotbugs" version '6.1.0'
    id "com.gorylenko.gradle-git-properties" version "2.4.2"
    id 'pmd'
    id "com.diffplug.spotless" version "7.0.2"
    id 'com.google.cloud.tools.jib' version '3.4.4'
}

version = '0.44.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'com.fasterxml.jackson.module:jackson-module-blackbird'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.freemarker:freemarker:2.3.34'
    implementation 'com.vladsch.flexmark:flexmark:0.64.8'
    implementation 'com.vladsch.flexmark:flexmark-ext-attributes:0.64.8'
    implementation 'com.vladsch.flexmark:flexmark-ext-definition:0.64.8'
    implementation 'com.vladsch.flexmark:flexmark-ext-tables:0.64.8'
    implementation 'io.github.openhtmltopdf:openhtmltopdf-core:1.1.24'
    implementation 'io.github.openhtmltopdf:openhtmltopdf-pdfbox:1.1.24'
    implementation 'io.github.openhtmltopdf:openhtmltopdf-slf4j:1.1.24'
    implementation 'io.github.openhtmltopdf:openhtmltopdf-objects:1.1.24'
    implementation ('org.apache.xmlgraphics:batik-transcoder:1.18'){
        // work around https://issues.apache.org/jira/browse/BATIK-1289
        exclude group: "xml-apis", module: "xml-apis"
    }
    implementation ('org.apache.xmlgraphics:batik-codec:1.18'){
        // work around https://issues.apache.org/jira/browse/BATIK-1289
        exclude group: "xml-apis", module: "xml-apis"
    }
    implementation 'org.jfree:jfreechart:1.5.5'
    implementation 'org.jsoup:jsoup:1.18.3'
    implementation 'com.helger.font:ph-fonts-api:5.0.3'

    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.8.6'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'

    runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
    runtimeOnly 'com.helger.font:ph-fonts-open-sans:5.0.3'
    runtimeOnly 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    runtimeOnly 'ch.qos.logback.contrib:logback-jackson:0.1.5'
}

// https://docs.spring.io/spring-framework/reference/core/spring-jcl.html#page-title
configurations.implementation {
    exclude group: 'commons-logging'
}

spotbugs.excludeFilter = rootProject.file('misc/spotbugs/exclude.xml')
spotbugsTest.enabled = false
// write human readable report on normal builds
spotbugsMain.reports {
    xml.enabled = false
    html.enabled = true
}

pmd {
    toolVersion = '7.9.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd-ruleset.xml')
    ruleSets = []
    rulesMinimumPriority = 3
    consoleOutput = true
}

pmdTest.enabled = false

springBoot {
    buildInfo {
        properties {
            if (rootProject.hasProperty('ciBuildNumber')) {
                additional = [
                    'ci.buildnumber': rootProject.getProperty('ciBuildNumber'),
                    'ci.jobname': rootProject.getProperty('ciJobName')
                ]
            }
        }
    }
}

bootRun {
    environment 'spring.output.ansi.console-available', true
}

jib {
    from {
        image = 'gcr.io/distroless/java21-debian12:nonroot'
    }
    container {
        labels = project.provider {
            [
                'org.opencontainers.image.title':"vernice.veo reporting",
                'org.opencontainers.image.description':"Backend of the verinice.veo-reporting web application.",
                'org.opencontainers.image.ref.name':'verinice.veo-reporting',
                'org.opencontainers.image.vendor':"SerNet GmbH",
                'org.opencontainers.image.authors':'verinice@sernet.de',
                'org.opencontainers.image.licenses':'AGPL-3.0',
                'org.opencontainers.image.source':'https://github.com/verinice/verinice-veo-reporting',
                'org.opencontainers.image.version': project.version,
                'org.opencontainers.image.revision': rootProject.getProperty('ciCommitId')
            ]
        }
        environment = ['JDK_JAVA_OPTIONS':'-Djdk.serialFilter=maxbytes=0']
        user = 'nonroot'
        ports = ['8080']
    }
}

testing {
    suites {
        test{
            useSpock('2.4-M1-groovy-4.0')
            dependencies {
                implementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation 'org.apache.groovy:groovy-json'
                implementation 'org.apache.groovy:groovy-xml'
            }
        }
    }
}

spotless {
    groovyGradle {
        greclipse()
        indentWithSpaces(4)
        replaceRegex('Excessive line breaks', /\n{3,}/, '\n\n')
        trimTrailingWhitespace()
    }
    java {
        target 'src/**/*.java', 'buildSrc/**/*.java'
        googleJavaFormat()
        importOrder('java', 'jakarta', 'javax', 'org', 'com', 'org.veo', '')
        addStep org.veo.LicenseHeaderStep.create(project.rootDir)
        addStep org.veo.NoWildcardImportsStep.create()
        removeUnusedImports()
        trimTrailingWhitespace()
        replaceRegex('Consecutive empty block comment lines', /( *\*\n){2,}/, '*\n')
        replaceRegex('Empty line at block comment end', / \*\n *\*\//, ' */')
        replaceRegex('Empty comment block', /\/\*+\s+\*\//, '')
        replaceRegex('Empty line after annotation', /(^ +@[a-zA-Z0-9]+\([^)]+\)\n)\n+/, '$1')
        replaceRegex('Empty line inside annotation', /(^ +@[a-zA-Z0-9]+\([^)]+\n)\n+/, '$1')
    }
    groovy {
        target 'src/**/*.groovy', 'buildSrc/**/*.groovy'
        addStep org.veo.LicenseHeaderStep.create(project.rootDir, 'package |runner \\{')
        addStep org.veo.NoWildcardImportsStep.create()
        greclipse()
        importOrder('java', 'javax', 'org', 'com', 'org.veo', '')
        indentWithSpaces(4)
        trimTrailingWhitespace()
        replaceRegex('Excessive line breaks', /\n{3,}/, '\n\n')
        replaceRegex('Extra space around equals sign', /(  += )|( =  +)/, ' = ')
        replaceRegex('Not one space between right round and left curly bracket', /\) *\{/, ') {')
        toggleOffOn()
    }
    format 'git', {
        target '**/.gitignore'

        trimTrailingWhitespace()
        endWithNewline()
    }
    json {
        target '**/*.json'
        targetExclude '**/bin/**', '**/target/**'
        gson()
                .indentWithSpaces(2)
        endWithNewline()
    }
    yaml {
        target '.gitlab-ci.yml'
        trimTrailingWhitespace()
        endWithNewline()
    }
}
